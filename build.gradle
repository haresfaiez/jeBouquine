apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'idea'

//TODO:configure versioning properly
version = '1.0'

def mainRepository = "http://localhost:8081/nexus/content/groups/public"
def acceptanceTestPkg = 'acceptance'
def acceptanceTestResources = 'src/test/resources/acceptance'

// dependencies versions
def junitVersion = '4.12'
def assertJVersion = '3.2.0'
def slf4jVersion = '1.7.12'
def hibernateVersion = '5.0.2.Final'
def cucumberVersion = '1.2.4'
def hsqldbVersion = '2.0.0'
def seleniumVersion = '2.48.2'
def springVersion = '4.2.3.RELEASE'

sourceSets {
    integrationtest {
        java.srcDir file('src/integrationtest/java')
        resources.srcDir file('src/integrationtest/resources')
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

task integrationtest(type: Test) {
    description = "Runs the integration tests"
    testClassesDir = sourceSets.integrationtest.output.classesDir
    classpath = sourceSets.integrationtest.runtimeClasspath
}

configurations {
    integrationtestCompile.extendsFrom testCompile
    integrationtestRuntime.extendsFrom testRuntime
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

task microtest(){

}

task staticCodeAnalysis(){

}

task release(){
 //TODO:deploy to artifact repository
}

task acceptancetest(type: Test) {
    description="acceptance test"
    dependsOn assemble, microtest
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', acceptanceTestPkg, acceptanceTestResources]
        }
    }
}

repositories {
    maven {
        url mainRepository
    }
}

dependencies {
    compile group: 'org.hibernate', name: 'hibernate-core', version: hibernateVersion
    compile group: 'org.hibernate', name: 'hibernate-entitymanager', version: hibernateVersion
    compile group: 'org.slf4j', name: 'slf4j-simple', version: slf4jVersion
    compile group: 'org.springframework', name: 'spring-webmvc',
            version: springVersion
    compile group: 'org.springframework', name: 'spring-web',
            version: springVersion
    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'


    testCompile group: 'junit', name: 'junit', version: junitVersion
    testCompile group: 'org.assertj', name: 'assertj-core', version: assertJVersion
    // Selenium
    testCompile group: 'org.seleniumhq.selenium', name: 'selenium-java', version:
            seleniumVersion
    testCompile group: 'org.seleniumhq.selenium', name:
            'selenium-firefox-driver', version: seleniumVersion
    // HsqlDb
    testCompile group: 'org.hsqldb', name: 'hsqldb', version: hsqldbVersion
    // Cucumber
    testCompile group: 'info.cukes', name: 'cucumber-java', version: cucumberVersion
    testCompile group: 'info.cukes', name: 'cucumber-junit', version: cucumberVersion
    //Spring test
    testCompile group: 'org.springframework', name: 'spring-test',
            version: springVersion

}