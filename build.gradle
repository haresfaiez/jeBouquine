apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'idea'
apply plugin: 'org.akhikhl.gretty'

//TODO:configure versioning properly
version = '1.0'

def mainRepository = "http://localhost:8081/nexus/content/groups/public"
def acceptanceTestPkg = 'acceptance'
def acceptanceTestResources = 'src/test/resources/acceptance'

def applicationURI = "jebouquine"

// dependencies versions
def junitVersion = '4.12'
def assertJVersion = '3.2.0'
def slf4jVersion = '1.7.12'
def hibernateVersion = '5.0.2.Final'
def cucumberVersion = '1.2.4'
def hsqldbVersion = '2.0.0'
def seleniumVersion = '2.48.2'
def springVersion = '4.2.3.RELEASE'
def servletApiVersion = '3.1.0'
def mockitoVersion = '1.10.19'
def hamcrestVersion = '1.3'
def dbUnitVersion = '2.5.1'
def springTestDBUnit = '1.2.1'
def thymeleafSpringVersion = '2.1.4.RELEASE'
def commonsDbcpVersion = '1.4'
def springSecurityVersion = '4.0.3.RELEASE'

sourceSets {
    acceptancetest {
        java.srcDir file('src/acceptancetest/java')
        resources.srcDir file('src/acceptancetest/resources')
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
    integrationtest {
        java.srcDir file('src/integrationtest/java')
        resources.srcDir file('src/integrationtest/resources')
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
    microtest {
        java.srcDir file('src/microtest/java')
        resources.srcDir file('src/microtest/resources')
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

task integrationtest(type: Test) {
    description = "Runs the integration tests"
    testClassesDir = sourceSets.integrationtest.output.classesDir
    classpath = sourceSets.integrationtest.runtimeClasspath
}

task microtest(type: Test) {
    description = "Runs the micro tests"
    testClassesDir = sourceSets.microtest.output.classesDir
    classpath = sourceSets.microtest.runtimeClasspath
}

configurations {
    integrationtestCompile.extendsFrom testCompile
    integrationtestRuntime.extendsFrom testRuntime
    microtestCompile.extendsFrom testCompile
    microtestRuntime.extendsFrom testRuntime
    acceptancetestCompile.extendsFrom testCompile
    acceptancetestRuntime.extendsFrom testRuntime
}


task acceptancetest(type: Test) {
    description="acceptance test"
    testClassesDir = sourceSets.acceptancetest.output.classesDir
    classpath = sourceSets.acceptancetest.runtimeClasspath
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            args = ['--plugin', 'pretty', '--glue', acceptanceTestPkg, acceptanceTestResources]
        }
    }
}

repositories {
    maven {
        url mainRepository
    }
    jcenter()
}

gretty {
    port = 8080
    contextPath = applicationURI
    servletContainer = 'jetty9' //tomcat7 or tomcat8
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.akhikhl.gretty:gretty:+'
    }
}

dependencies {
    compile group: 'org.hibernate', name: 'hibernate-core', version: hibernateVersion
    compile group: 'org.hibernate', name: 'hibernate-entitymanager', version: hibernateVersion
    compile group: 'org.slf4j', name: 'slf4j-simple', version: slf4jVersion
    compile group: 'org.springframework', name: 'spring-webmvc',
            version: springVersion
    compile group: 'org.springframework', name: 'spring-web',
            version: springVersion
     compile group: 'org.springframework', name: 'spring-orm',
            version: springVersion
    compile group: 'org.springframework', name: 'spring-tx',
            version: springVersion
    compile group: 'org.thymeleaf', name: 'thymeleaf-spring4',
            version: thymeleafSpringVersion
    compile group: 'commons-dbcp', name: 'commons-dbcp',
            version: commonsDbcpVersion
    compile group: 'org.springframework.security', name: 'spring-security-web',
            version: springSecurityVersion
    compile group: 'org.springframework.security', name: 'spring-security-config',
            version: springSecurityVersion

    providedCompile group: 'javax.servlet', name: 'javax.servlet-api',
            version: servletApiVersion

    testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: hamcrestVersion
    testCompile group: 'junit', name: 'junit', version: junitVersion
    testCompile group: 'org.assertj', name: 'assertj-core', version: assertJVersion

    // HsqlDb
    providedCompile group: 'org.hsqldb', name: 'hsqldb', version: hsqldbVersion
    // Cucumber
    testCompile group: 'info.cukes', name: 'cucumber-java', version: cucumberVersion
    testCompile group: 'info.cukes', name: 'cucumber-junit', version: cucumberVersion
    // Selenium
    testCompile group: 'org.seleniumhq.selenium', name: 'selenium-java', version:
            seleniumVersion
    testCompile group: 'org.seleniumhq.selenium', name:
            'selenium-firefox-driver', version: seleniumVersion
    //Spring test
    testCompile group: 'org.springframework', name: 'spring-test',
            version: springVersion
    //Mockito
    testCompile group: 'org.mockito', name: 'mockito-all',
            version: mockitoVersion
    //DBUnit
    testCompile group: 'org.dbunit', name: 'dbunit',
            version: dbUnitVersion
    testCompile group: 'com.github.springtestdbunit', name: 'spring-test-dbunit',
            version: springTestDBUnit
}
