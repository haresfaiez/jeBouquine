apply plugin: 'java'
apply plugin: 'idea'

version = '1.0'

def mainRepository = "http://localhost:8081/nexus/content/groups/public"
def acceptanceTestPkg = 'acceptance.book.search'
def acceptanceTestRes = 'src/test/resources'
// dependencies versions
def junitVersion = '4.12'
def assertJVersion = '3.2.0'
def slf4jVersion = '1.7.12'
def hibernateVersion = '5.0.2.Final'
def cucumberVersion = '1.2.4'
def hsqldbVersion = '2.0.0'

configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

task cucumber() {
    dependsOn assemble, compileTestJava
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', acceptanceTestPkg, acceptanceTestRes]
        }
    }
}

repositories {
    maven {
        url mainRepository
    }
}

dependencies {
    compile group: 'org.hibernate', name: 'hibernate-core', version: hibernateVersion

    testCompile group: 'junit', name: 'junit', version: junitVersion
    testCompile group: 'org.assertj', name: 'assertj-core', version: assertJVersion
    testCompile group: 'org.slf4j', name: 'slf4j-simple', version: slf4jVersion
    testCompile group: 'info.cukes', name: 'cucumber-java', version: cucumberVersion
    testCompile group: 'info.cukes', name: 'cucumber-junit', version: cucumberVersion
    testCompile group: 'org.hsqldb', name: 'hsqldb', version: hsqldbVersion
}